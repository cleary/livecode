# MiniTidal Audio Workshop 1-2 - Mini-notation Sequencing and Patterning Concepts (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - http://tidalcycles.org/docs/

## Setup Cells!

`!presetview twocolumns`

Choose MiniTidal from the language dropdown for both cells

## Introduction to "mini-notation" for sequencing

**Mini-notation** is the language within tidal that is used to "sequence" the sounds ie specifying the **order** and **position in time** at which the sound plays.  

Whenever you see `" "` in tidal, the language between the quotes is mini-notation

Let's start in the left hand cell, and create a simple kick snare loop

```
sound "bd sd"
```

The more samples we add, the faster the items in the sequence plays - tidal fits all the sounds within the "cycle" which remains constant

```
sound "bd hc sd hc bd bd hc sd ho"
```

In `1-1` we also began exploring the concept of applying some punctuation operators to samples - let's revisit 

Starting with a simple pattern again, let's use the `*` operator

```
sound "bd*2 sd"
```

`*N` will repeat the sample the number of times you choose within the time that would be allocated to a single `bd` event, or a **"step"** - try increasing the number from `2`

`!N` is a similar operator, but subtly different - it also tells the sample to repeat the samples by the number specified, but spreads the time allocated for each "event" evenly within the cycle ie inserts extra **"steps"**

```
sound "bd!2 sd"
```

This is equivalent to `"bd bd sd"` Check it! Then try increasing the number from `2`

`/N` will reduce the number of times the event plays to every `N`th cycle

```
sound "bd/2 sd"
```

Notice how the `bd` sample only plays every `2`nd cycle?

Strictly speaking, the `/` operator will spread the sequence it is working on over the number of cycles you specify - we'll see how it can be applied to a (sub-) sequence further on

## Sub-sequencing with `[ ]`

Mini-notation makes heavy use of different sorts of bracket types, each has a very specific purpose

`[ ]` or "square brackets" group a number of events into a step, creating a **sub-sequence** - let's start with a new simple pattern again

```
sound "bd hc sd hc"
```

and fit some extra events (`~ bd`) into the last step

```
sound "bd hc sd [hc ~ bd]"
```

You can nest square brackets

```
sound "bd hc sd [[hc bd] ~ [bd*2]]"
```

...and easily create **"compound time signatures"**

```
sound "[bd hc sd] [hc bd hc bd]"
```

You can even apply the mathematical operators mentioned earlier to bracketed sub-sequences

```
sound "bd hc sd [hc ~ bd]*2"
```
```
sound "bd hc sd [hc ~ bd]!2"
```
```
-- or equally
sound "bd hc sd [hc ~ bd] !"
```
```
sound "bd hc sd [hc ~ bd]/3"
```

*Note: the `/` operator is actually spreading the `[hc ~ bd]` pattern evenly over the three "steps" in the three "cycles"... what if you change the `3` to a `2`? /cue brain explosion*

Square brackets allow us to add multiple simultaneous patterns to easily create **polyrhythms!**

```
sound "[bd bd, sd sd sd, hc hc hc hc]"
```

## Polymeter with `{ }`

In the previous `[ ]` example, each of the different rhythms fit itself within the cycle, without reference to any other pattern or rhythm - multiple rhythms played together like this are a **"polyrhythm"**.

If we had the pattern `[a b, c d e]`, it would sequence a **polyrhythm** like this:

```
|a--b--|a--b--|
|c-d-e-|c-d-e-|
```

A **"polymeter"** occurs when those different length patterns align their steps, but due to the differing pattern lengths they phase in and out of each other

Using the same pattern, but with *curly braces* `{a b, c d e}` creates a **polymeter** sequence like this

```
|ab|ab|ab|ab|ab|ab|
|cd|ec|de|cd|ec|de|
```

*Notice the pattern combination takes 3 cycles to repeat*

How does it sound? Listen for the double kick to indicate the second pattern endpoint

```
sound "{bd sd, hc cr hc ho bd*2}"
```

By default, the number of steps that the **polymeter** patterns align to is be defined by the steps in the first pattern.

`%N` can be appended to the braces to manually set the number of steps for alignment

```
s "{bd sd, hc sd hc sd bd*2}%4"
```

## Patterns per cycle with `< >`

Often you will not want the pattern to repeat *every* cycle, and to assist with this are the handy "Angle Brackets"

Space separated patterns within `< >` brackets will iterate through each space separated pattern, each cycle

```
sound "<bd cp bd [cp ho]>"
```

They can also be nested

```
sound "<<bd*4 sd*12> [bd hc]*4 >"
```
## `n` Notation for Sample Index References

In `1-1` we learned that different samples can be chosen from the sample folders using `:N`

```
sound "bd:3 hc sd:1 hc bd bd:3 hc sd ho:4"
```

If all the sounds come from the same folder, it's easier to pattern using `n` notation - `sprvibe` is a series of ascending notes sampled from the "supervibe" synth

```
n "0 3 [4 5] [8 7]" # sound "sprvibe"
```

This is equivalent to:
```
sound "sprvibe:0 sprvibe:3 [sprvibe:4 sprvibe:5] [sprvibe:8 sprvibe:7]"
```

...but much more concise! Also, we can do some funky arithmetic with `n`
```
n ("0 3 [4 5] [8 7]" + "0 2") # sound "sprvibe"
```

Try changing the additional section and evaluating **live**
```
-- example
n ("0 3 [4 5] [8 7]" + "2 4 ~") # sound "sprvibe"
```

Because we're putting our additional section in `" "`, we can also use mini-notation. Try using different brackets in there
```
-- example
n ("0 3 [4 5] [8 7]" + "{2 4 ~}%2") # sound "sprvibe"
```

You can **modulate** the pitch on a per cycle basis using `< >`
```
-- example
n ("0 3 [4 5] [8 7]" + "<0!3 2>") # sound "sprvibe"
```

---

# Exercises:

1. Shorten the following examples using any of the operators, brackets and/or `n` notation covered in this workshop

*Tip: put the original pattern in one cell, and build your variation in the other cell so you can play them both to confirm* 

```
sound "[bd bd] hc sd hc bd hs sd [hc hc [hc hc]]"

sound "bd hc sd hc bd hs sd hc"

sound "bd hc sd [[hc ~ bd] [hc ~ bd]]"

sound "[glitch:0 glitch:1 glitch:1] glitch:5 ~ [glitch:6 glitch:5*2]"
```

## For Bonus Points

2. Samples can be pitch adjusted using `note` in the same format as `n` notation, investigate using the single `gtr:0` sample
```
note ("0 3 [4 5] [8 7]" + "<0!3 2>") # sound "gtr:0"
```

---
